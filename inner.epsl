%import irgen;
%import eewriter;
%import wrapper;

State {
    Str:filename,
    [Func_]:funcs,
    [ExternFunc]:extern_funcs,
    [Func_]:func_stack,
    Str?:next_fn_specifiers,
    [Str]?:next_fn_attrs,
    ExternFunc?:last_defined_func,
    [Struct_]:structs,
    [IRGlobal]:globals,
    Val?:error_stack_top
}

// in the future this will @extend ExternFunc
Func_ {
    ExternFunc:base,
    [Block]:block_stack,
    IRBasicBlock:current_bb,
    Val?:last_ret_val,
}

%global State?:state;

State#get state {
    if (state) {
        return [state].unwrap;
    } else {
        abort ["State not initialized"];
    };
}

#init state [Str:filename] {
    state = State [
        filename, [Func_] [], [ExternFunc] [], [Func_] [], null, null, null,
        [Struct_] [], [IRGlobal] [], null
    ];
}

IRStructDef#[Struct_:struct_].ir {
    [$IRType]:members = [$IRType] [];
    if (struct_.is_ref) {
        // add the ref counter field
        [members].append[IRIntType [64]];
    };
    for (Field:field in struct_.fields) {
        [members].append[[field.type_].ir];
    };
    return IRStructDef [struct_.symbol, false, IRStructType [members]];
}

IRModule#build module {
    State:state = get state;
    [IRStructDef]:ir_structs = [IRStructDef] [];
    for (Struct_:struct_ in state.structs) {
        [ir_structs].append[[struct_].ir];
    };
    [IRFunc]:ir_funcs = [IRFunc] [];
    for (ExternFunc:extern_func in state.extern_funcs) {
        [ir_funcs].append[extern_func.ir_func];
    };
    for (Func_:func in state.funcs) {
        [ir_funcs].append[func.base.ir_func];
    };
    return IRModule [
        "", "", state.filename, state.globals, ir_structs, ir_funcs
    ];
}

#clear state {
    state = null;
}

Func_#get active func {
    [Func_]:func_stack = get state.func_stack;
    if ([func_stack].len == 0) {
        abort ["Code cannot be emitted while not in a function"];
    };
    return [func_stack].at[-1];
}

ExternFunc?#get func named [Str:name] {
    for (Func_:func in get state.funcs) {
        if ([func.base.ir_func.name] equals [name]) {
            return func.base;
        };
    };

    for (ExternFunc:extern_func in get state.extern_funcs) {
        if ([extern_func.ir_func.name] equals [name]) {
            return extern_func;
        };
    };

    return null;
}

Val#get error stack top {
    State:state = get state;
    given (state.error_stack_top as Val:error_stack_top) {
        return error_stack_top;
    };
    Val:error_stack_top = extern global [Pointer [Pointer [Byte_]]]
        ["epsl_error_stack_top"];
    state.error_stack_top = error_stack_top;
    return error_stack_top;
}

Str#next fn specifiers {
    Str?:specifiers = get state.next_fn_specifiers;
    get state.next_fn_specifiers = null;
    return [specifiers] || [""];
}

[Str]#next fn attrs {
    [Str]?:attrs = get state.next_fn_attrs;
    get state.next_fn_attrs = null;
    return [attrs] || [[Str] []];
}

[Str]#get type_ attrs [Type_:type_] {
    if ([type_.name] equals ["Null"]) {
        return [Str] [];
    };
    [Str]:attrs = [Str] ["noundef"];
    if ([type_].has_pointee) {
        L:min_sizeof = get min comptime sizeof [[type_].pointee];
        if (min_sizeof == 0) {

        } elif ([type_].is_nullable) {
            [attrs].append["dereferenceable_or_null({})" % min_sizeof];
        } else {
            [attrs].append["dereferenceable({})" % min_sizeof];
        };
    };
    return attrs;
}

$IRType#[Type_:type_].ir_ret_type_ {
    if ([type_.name] equals ["Null"]) {
        return IRVoidType [];
    } else {
        return [type_].ir;
    };
}

ExternFunc#create new extern fn [Type_:ret_type_] [Str:name] [[Type_]:arg_types_] [Bool:has_vargs] specifiers: [Str:specifiers] fn_attrs: [[Str]:fn_attrs] {
    [IRArgument]:args = [IRArgument] [];
    for (W:i enumerating arg_types_) {
        Type_:arg_type_ = arg_types_[i];
        [args].append[IRArgument [[arg_type_].ir, get type_ attrs [arg_type_], "arg{}" % i]];
    };
    IRReturnType:ret = IRReturnType [[ret_type_].ir_ret_type_, get type_ attrs [ret_type_]];
    IRFunc:ir = IRFunc [ret, specifiers, name, args, has_vargs, fn_attrs, null];
    return ExternFunc [ir, ret_type_, arg_types_];
}

Bool#extern func matches [ExternFunc:fn] [Type_:ret_type_] [Str:name] [[Type_]:arg_types_] [Bool:has_vargs] {
    if ([fn.ir_func.name] not equals [name]) {
        return false;
    };
    if ([fn.ir_func.has_vargs] ^ [has_vargs]) {
        return false;
    };
    if (![fn.ret_type_] == [ret_type_]) {
        return false;
    };
    if ([fn.args].len != [arg_types_].len) {
        return false;
    };
    for (W:i enumerating arg_types_) {
        if (![fn.args[i]] == [arg_types_[i]]) {
            return false;
        };
    };
    return true;
}

ExternFunc#require extern fn [Type_:ret_type_] [Str:name] [[Type_]:arg_types_] [Bool:has_vargs] {
    Str:specifiers = next fn specifiers;
    [Str]:fn_attrs = next fn attrs;
    ExternFunc?:maybe_fn = get func named [name];
    given (maybe_fn as ExternFunc:fn) {
        if (extern func matches [fn] [ret_type_] [name] [arg_types_] [has_vargs]) {
            get state.last_defined_func = fn;
            return fn;
        } else {
            abort ["An extern function already exists by the name {}, but does not share the same return type_ or parameters" % name];
        };
    };
    ExternFunc:extern_func = create new extern fn [ret_type_] [name] [arg_types_] [has_vargs] specifiers: [specifiers] fn_attrs: [fn_attrs];
    [get state.extern_funcs].append[extern_func];
    get state.last_defined_func = extern_func;
    return extern_func;
}

Func_?#defined fn [Type_:ret_type_] [Str:name] [[Type_]:arg_types_] {
    for (Func_:func in get state.funcs) {
        if ([func.base.ir_func.name] equals [name]) {
            if (extern func matches [func.base] [ret_type_] [name] [arg_types_] [false]) {
                return func;
            } else {
                abort ["A function already exists with the name {}, but does not share the same return type_ or parameters" % name];
            };
        };
    };

    return null;
}

Func_#new fn [Type_:ret_type_] [Str:name] [[Type_]:arg_types_] specifiers: [Str:specifiers] fn_attrs: [[Str]:fn_attrs] {
    [ExternFunc]:extern_funcs = get state.extern_funcs;
    for (W:i enumerating extern_funcs step -1) {
        ExternFunc:extern_func = extern_funcs[i];
        if ([extern_func.ir_func.name] equals [name]) {
            if (extern func matches [extern_func] [ret_type_] [name] [arg_types_] [false]) {
                [extern_funcs].pop[i];
            } else {
                abort ["An extern function already exists with the name {}, but has the incorrect prototype" % name];
            };
            break;
        };
    };

    ExternFunc:base = create new extern fn [ret_type_] [name] [arg_types_] [false] specifiers: [specifiers] fn_attrs: [fn_attrs];
    IRBasicBlock:entry_bb = IRBasicBlock [0, [$IRInstruction] [], false];
    [IRBasicBlock]:basic_blocks = [IRBasicBlock] [entry_bb];
    base.ir_func.body = IRFuncBody [basic_blocks, 1];
    return Func_ [base, [Block] [], entry_bb, null];
}

#add fn [Func_:func] {
    State:state = get state;
    [state.func_stack].append[func];
    [state.funcs].append[func];
}

#add block [Block:block] to stack {
    [get active func.block_stack].append[block];
}

Block#pop from block stack {
    [Block]:block_stack = get active func.block_stack;
    return [block_stack].pop_end;
}

$IRType#[Type_:type_].ir {
    if (([type_.name] equals ["Bool"]) || ([type_.name] equals ["Byte"]) || ([type_.name] equals ["W"])
        || ([type_.name] equals ["Z"])) {
        return IRIntType [type_.bits#W];
    } elif ([type_.name] equals ["R"]) {
        return IRFloatType [type_.bits#W];
    } elif ([type_.name] equals ["Null"]) {
        return IRIntType [1];
    } elif (([type_.name] equals ["Internal"])
        || ([type_.name] equals ["Array"]) || ([type_.name] equals ["#Pointer"])
        || ([type_.name] equals ["#FuncPtr"])
        || ([type_.name] equals ["#StructBase"])) {
        return IRPtrType [];
    } elif ([type_].is_poly) {
        return IRStructType [[$IRType] [IRPtrType [], IRPtrType []]];
    } elif ([type_.name] equals ["Optional"]) {
        Type_:generic = type_.generics[0];
        if ([generic].is_nonnull_ptr) {
            return IRPtrType [];
        } elif ([generic].is_poly) {
            return [generic].ir;
        } else {
            return IRStructType [[$IRType] [
                IRIntType [1],
                [generic].ir
            ]];
        };
    } elif ([type_.name] equals ["#VTable"]) {
        return IRStructType [[$IRType] [
            IRIntType [64], // struct id number
            IRIntType [64], // the length of the extendee list
            IRPtrType [], // the extendee list
            IRPtrType [], // the struct id (as a Str)
            IRPtrType [] // the pointer to the free function
        ]];
    } else {
        Struct_?:maybe_struct = [type_].get_struct;

        if (!maybe_struct) {
            abort ["Unknown type_ {}" % [type_].stringify];
        };

        Struct_:struct_ = [maybe_struct].unwrap;
        if (struct_.is_ref) {
            return IRPtrType [];
        } else {
            [$IRType]:members = [$IRType] [];
            [members].require_capacity[[struct_.fields].len];
            for (Field:field in struct_.fields) {
                [members].append[[field.type_].ir];
            };
            return IRStructType [members];
        };
    };
}

Bool#[Type_:type_].is_ptr {
    if (([type_.name] equals ["Bool"]) || ([type_.name] equals ["Byte"]) || ([type_.name] equals ["W"])
        || ([type_.name] equals ["Z"]) || ([type_.name] equals ["R"]) || ([type_.name] equals ["Null"])
        || [type_].is_poly) {
        return false;
    } elif ([type_.name] equals ["Optional"]) {
        return [type_.generics[0]].is_ptr;
    } else {
        given [type_].get_struct as Struct_:struct_ {
            return struct_.is_ref;
        };
        return true;
    };
}

Bool#[Type_:type_].is_nonnull_ptr {
    return [type_].is_ptr && ![type_].is_nullable;
}

Bool#[Type_:type_].has_pointee {
    return [type_].is_ptr && !(
        ([type_.name] equals ["Internal"]) || ([type_.name] equals ["#FuncPtr"]));
}

$IRType#[Type_:type_].pointee {
    if ([type_.name] equals ["Array"]) {
        return IRStructType [[$IRType] [IRIntType [64], IRIntType [64], IRIntType [64], IRPtrType []]];
    } elif ([type_.name] equals ["#Pointer"]) {
        return [type_.generics[0]].ir;
    } elif ([type_.name] equals ["Optional"]) {
        return [type_.generics[0]].pointee;
    } else {
        Struct_?:maybe_struct = [type_].get_struct;

        given (maybe_struct as Struct_:struct_) {
            if (!struct_.is_ref) {
                abort ["Only ref structs have a pointee type, {} is not a ref struct" % struct_.id];
            };
            [$IRType]:members = [$IRType] [IRIntType [64] /* the ref counter */];
            [members].require_capacity[[struct_.fields].len+1];
            for (Field:field in struct_.fields) {
                [members].append[[field.type_].ir];
            };
            return IRStructType [members];
        };

        abort ["Type_ {} does not have a pointee IRType" % [type_].stringify];
    };
}

L#get min comptime sizeof [$IRType:type] {
    given (type as IRIntType:int_t) {
        return int_t.bits ~/ 8;
    } given (type as IRFloatType:float_t) {
        return float_t.bits ~/ 8;
    } given (type as IRArrayType:arr_t) {
        return arr_t.elem_count * get min comptime sizeof [arr_t.elem_type];
    } given (type as IRStructType:struct_t) {
        L:total = 0;
        for ($IRType:member in struct_t.members) {
            total += get min comptime sizeof [member];
        };
        return total;
    };
    return 0;
}

IRValue#[Val:val].get {
    given (val.basic as BasicVal:basic_val) {
        return basic_val.val;
    } given (val.assignable as AssignableVal:assignable_val) {
        return emit [load [assignable_val.addr] to [[val.type_].ir]];
    } given (val.const as ConstVal:const_val) {
        return const_val.val;
    } else {
        abort ["Invalid Val"];
    };
}

IRBasicBlock#current bb {
    return get active func.current_bb;
}

IRBasicBlock#new bb {
    IRBasicBlock:bb = IRBasicBlock [next id, [$IRInstruction] [], false];
    [[get active func.base.ir_func.body].unwrap.blocks].append[bb];
    return bb;
}

#set current bb [IRBasicBlock:bb] {
    get active func.current_bb = bb;
}

W#next id {
    return [get active func.base.ir_func.body].unwrap.id_counter++;
}

Str#name register [W:id] {
    return ".{}" % id;
}

IRRegister#new register [$IRType:type] {
    return IRRegister [false, name register [next id], type];
}

IRRegister#new register [Type_:type] {
    return IRRegister [false, name register [next id], [type].ir];
}

IRValue#make_val [IRRegister:reg] {
    return IRValue [reg, null];
}

IRValue#make_val [$IRValuedInstruction:valued] {
    return make_val [valued.register];
}

IRValue#emit [$IRValuedInstruction:valued] {
    [current bb.instructions].append[valued];
    return make_val [valued];
}

#emit [$IRInstruction:instruction] {
    [current bb.instructions].append[instruction];
    if (instruction#IRRetVoidInstruction? || instruction#IRRetInstruction?
        || instruction#IRConditionalBranchInstruction? || instruction#IRUnconditionalBranchInstruction?
        || instruction#IRSwitchInstruction? || instruction#IRUnreachableInstruction?) {
        current bb.is_terminated = true;
    };
}

#emit [$IRInstruction:instruction] unless terminated {
    if (!current bb.is_terminated) {
        emit [instruction];
    };
}

IRRegister#register of func [IRFunc:func] {
    return IRRegister [true, func.name, IRPtrType []];
}

IRValue#[IRFunc:func].make_ptr {
    return make const [register of func [func]];
}

Val#basic val [Type_:type_] [IRValue:val] {
    return Val [type_, BasicVal [val, current bb], null, null];
}

Val#assignable val [Type_:type_] [IRValue:addr] {
    return Val [type_, null, AssignableVal [addr], null];
}

Val#const val [Type_:type_] [IRValue:val] {
    return Val [type_, null, null, ConstVal [val]];
}

[Str]#get instr flags [Type_:type_] bound: [Bool:bound] {
    if (bound) {
        if ([type_].is_signed_int) {
            return [Str] ["nsw"];
        } elif ([type_].is_unsigned_int) {
            return [Str] ["nuw"];
        };
    };
    return [Str] [];
    
}

IRValue#make nonref struct_ [[IRValue]:elems] {
    [$IRType]:member_types_ = [$IRType] [];
    for (IRValue:elem in elems) {
        [member_types_].append[[elem].type];
    };
    IRValue:struct_ = make const [IRStructType [member_types_]] poison;
    for (W:i enumerating elems) {
        struct_ = emit [insert value [elems[i]] [struct_] [[W] [i]]];
    };
    return struct_;
}

IRValue#make const nonref struct_ [[IRValue]:elements] {
    [$IRConstant]:const_elements = [$IRConstant] [];
    [$IRType]:elem_types = [$IRType] [];
    for (IRValue:element in elements) {
        $IRConstant:const_elem = [element.constant]
            .expect["All elements must be constants"];
        [const_elements].append[const_elem];
        [elem_types].append[const_elem.type];
    };
    return make const [IRStructType [elem_types]] [const_elements];
}

#call [Type_:ret_type_] [IRRegister:register] [[Type_]:arg_types_] [[Val]:args] {
    [IRValue]:arg_vals = [IRValue] [];
    if ([args].len < [arg_types_].len) {
        abort ["Not enough arguments for function"];
    };
    for (W:i enumerating args) {
        if (i < [arg_types_].len) {
            if (![args[i].type_] == [arg_types_[i]]) {
                abort ["Incorrect type_ passed as argument to function"];
            };
        };
        [arg_vals].append[[args[i]].get];
    };

    W:varg_count = ([args].len - [arg_types_].len)#W;

    if ([ret_type_.name] equals ["Null"]) {
        emit [call void [register] [arg_vals] [varg_count]];
        get active func.last_ret_val = c null_ [Null_];
    } else {
        IRValue:result = emit [call [[ret_type_].ir] [register] [arg_vals] [varg_count]];
        get active func.last_ret_val = basic val [ret_type_] [result];
    };
}

IRValue#bitwise not [Type_:type_] [IRValue:val] {
    return emit [IRXORInstruction [
        new register [type_], [Str] [], val, make const [-1#L] [type_.bits#W]
    ]];
}

IRValue#[Type_:type_] [IRValue:val] == null_ {
    if ([type_.name] equals ["Internal"]) {
        return emit [compare int [val] ["eq"] [make const null_ ptr]];
    };
    assert [type_] is optional;
    Type_:generic = type_.generics[0];
    if ([generic].is_nonnull_ptr) {
        return [Internal_] [cast [val] from [type_] to [Internal_]] == null_;
    } elif ([generic].is_poly) {
        Val:struct_val = [basic val [generic] [val]].struct_;
        return [Internal_] [[cast [struct_val] to [Internal_]].get] == null_;
    } else {
        IRValue:is_null = extract field [0] of [val] as [IRIntType [1]];
        return bitwise not [Bool_] [is_null];
    };
}

IRValue#[Type_:type_] [IRValue:val] != null_ {
    if ([type_.name] equals ["Internal"]) {
        return emit [compare int [val] ["ne"] [make const null_ ptr]];
    };
    assert [type_] is optional;
    Type_:generic = type_.generics[0];
    if ([generic].is_nonnull_ptr) {
        return [Internal_] [cast [val] from [type_] to [Internal_]] != null_;
    } elif ([generic].is_poly) {
        Val:struct_val = [basic val [generic] [val]].struct_;
        return [Internal_] [[cast [struct_val] to [Internal_]].get] != null_;
    } else {
        return extract field [0] of [val] as [IRIntType [1]];
    };
}

IRValue#compute truth value [Type_:type_] [IRValue:val] {
    if ([type_].is_int && type_.bits == 1) {
        return val;
    } elif ([type_.name] equals ["Optional"]) {
        return [type_] [val] != null_;
    } elif ([type_.name] equals ["Array"]) {
        IRValue:length = [[basic val [type_] [val]].length].get;
        return emit [compare int [length] ["ne"] [make const [0] [64]]];
    } elif ([type_].is_int) {
        return emit [compare int [val] ["ne"] [make const [0] [type_.bits#W]]];
    } elif ([type_].is_float) {
        return emit [compare float [val] ["one"] [make const [0.0] [type_.bits#W]]];
    } elif ([type_.name] equals ["Null"]) {
        return make const [false];
    } elif ([type_.name] equals ["Internal"]) {
        return make const [true];
    } elif ([type_].get_struct) {
        return make const [true];
    };

    abort ["Don't know how to compute the truth value of type_ {}" % [type_].stringify];
}

IRValue#compute untruth value [Type_:type_] [IRValue:val] {
    if ([type_].is_int && type_.bits == 1) {
        return bitwise not [type_] [val];
    } elif ([type_.name] equals ["Optional"]) {
        return [type_] [val] == null_;
    } elif ([type_.name] equals ["Array"]) {
        IRValue:length = [[basic val [type_] [val]].length].get;
        return emit [compare int [length] ["eq"] [make const [0] [64]]];
    } elif ([type_].is_int) {
        return emit [compare int [val] ["eq"] [make const [0] [type_.bits#W]]];
    } elif ([type_].is_float) {
        return emit [compare float [val] ["oeq"] [make const [0.0] [type_.bits#W]]];
    } elif ([type_.name] equals ["Null"]) {
        return make const [true];
    } elif ([type_.name] equals ["Internal"]) {
        return make const [false];
    } elif ([type_].get_struct) {
        return make const [false];
    };

    abort ["Don't know how to compute the untruth value of type_ {}" % [type_].stringify];
}

IRValue#new poly [Type_:type_] [IRValue:val] {
    return make nonref struct_ [[IRValue] [
        val, [&[vtable for_ [type_]]].get
    ]];
}

IRValue#convert integer [IRValue:val] bits from [Type_:from] to [Type_:to] {
    if (to.bits > from.bits) {
        if ([from].is_signed_int) {
            return emit [sign extend [val] to [[to].ir]];
        } else {
            return emit [zero extend [val] to [[to].ir]];
        };
    } elif (to.bits < from.bits) {
        return emit [truncate [val] to [[to].ir]];
    } else {
        return val;
    };
}

IRValue#convert float [IRValue:val] bits from [Type_:from] to [Type_:to] {
    if (to.bits > from.bits) {
        return emit [float extend [val] to [[to].ir]];
    } elif (to.bits < from.bits) {
        return emit [float truncate [val] to [[to].ir]];
    } else {
        return val;
    };
}

IRValue#wrap [IRValue:val] to optional type_ [Type_:to] {
    Type_:generic = to.generics[0];
    if (([generic].is_nonnull_ptr) || ([generic].is_poly)) {
        return val;
    } else {
        return make nonref struct_ [[IRValue] [
            make const [true], val
        ]];
    };
}

IRValue#unwrap optional [IRValue:val] of type_ [Type_:from] {
    Type_:generic = from.generics[0];
    if (([generic].is_nonnull_ptr) || ([generic].is_poly)) {
        return val;
    } else {
        return extract field [1] of [val] as [[generic].ir];
    };
}

IRValue#cast poly [IRValue:val] of type_ [Type_:from] to IRStructType_ {
    return [[basic val [from] [val]].struct_].get;
}

IRValue#cast poly [IRValue:val] of type_ [Type_:from] to optional type_ [Type_:to] {
    Type_:generic = to.generics[0];
    if ([generic.name] equals ["Struct"]) {
        return val;
    } elif ([generic.name] equals ["Poly"]) {
        Struct_:to_struct_ = assert [generic.generics[0]] is struct_;

        // if from IS Struct, then this will always be a downcast
        if ([from.name] not equals ["Struct"]) {
            Struct_:from_struct_ = assert [from.generics[0]] is struct_;
            
            Bool:is_nocast = from_struct_.id == to_struct_.id;
            Bool:is_upcast = [from_struct_] extends [to_struct_];
            if (is_nocast || is_upcast) {
                return val;
            };
        };

        // it must logically be a downcast
        Val:typed_val = basic val [from] [val];
        Val:does_extend = does [typed_val] extend [to_struct_];

        PHINode:result_phi = new PHI [to];
        [result_phi].add_incoming[c null_ [to]];
        if_ [does_extend];
            [result_phi].add_incoming[bitcast [typed_val] to [to]];
        end block;

        return [[result_phi].place_here].get;
    } else {
        Struct_:struct_ = assert [generic] is struct_;

        PHINode:result_phi = new PHI [to];
        [result_phi].add_incoming[c null_ [to]];

        Val:typed_val = basic val [from] [val];
        Val:struct_id = [[typed_val].vtable].vtable_item[0];
        if_ [[struct_id] eq [c L_ [struct_.id_num]]];
            [result_phi].add_incoming[bitcast [[typed_val].struct_] to [to]];
        end block;

        return [[result_phi].place_here].get;
    };
}

IRValue#cast [IRValue:val] of type_ [Type_:from] to poly type_ {
    if ([from].is_poly) {
        return val;
    } else {
        return new poly [from] [val];
    };
}

IRValue#cast optional [IRValue:val] from [Type_:from] to [Type_:to] {
    PHINode:result_phi = new PHI [to];
    // if the value is null, don't change it
    [result_phi].add_incoming[c null_ [to]];
    if_ [basic val [Bool_] [[from] [val] != null_]];
        [result_phi].add_incoming[basic val [to] [
            cast [val]
            from [unwrap nullability [from]]
            to [to]
        ]];
    end block;
    return [[result_phi].place_here].get;
}

IRValue#cast [IRValue:val] from [Type_:from] to optional [Type_:to] {
    return wrap [cast [val] from [from] to [unwrap nullability [to]]]
        to optional type_ [to];
}

IRValue#cast [IRValue:val] from [Type_:from] to [Type_:to] {
    if ([from] == [to]) {
        return val;
    } elif ([from.name] equals ["Null"]) {
        return [c null_ [to]].get;
    } elif ([to.name] equals ["Bool"]) {
        return compute truth value [from] [val];

    } elif ([from.name] equals ["#Pointer"]) {
        return val;
    } elif ([to.name] equals ["#Pointer"]) {
        return val;
    } elif ([from.name] equals ["Internal"]) {
        return val;
    } elif ([to.name] equals ["Internal"]) {
        return val;
    } elif (([from].is_callable) && ([to].is_callable)) {
        return val;

    } elif (([from.name] equals ["Optional"]) && ([from.generics[0]] == [to])) {
        return unwrap optional [val] of type_ [from];
    
    } elif ([from].is_int && [to].is_int) {
        return convert integer [val] bits from [from] to [to];
    } elif ([from].is_float && [to].is_float) {
        return convert float [val] bits from [from] to [to];
    } elif ([from].is_float && [to].is_unsigned_int) {
        return emit [float [val] to unsigned [[to].ir]];
    } elif ([from].is_float && [to].is_signed_int) {
        return emit [float [val] to signed [[to].ir]];
    } elif ([from].is_unsigned_int && [to].is_float) {
        return emit [unsigned [val] to float [[to].ir]];
    } elif ([from].is_signed_int && [to].is_float) {
        return emit [signed [val] to float [[to].ir]];

    } elif (([from.name] equals ["Poly"]) && ([from.generics[0]] == [to])) {
        return cast poly [val] of type_ [from] to IRStructType_;
    } elif ([from].is_poly && ([to.name] equals ["Optional"])) {
        return cast poly [val] of type_ [from] to optional type_ [to];
    } elif ([to].is_poly) {
        return cast [val] of type_ [from] to poly type_;

    } elif (([to.name] equals ["Optional"]) && ([from] == [to.generics[0]])) {
        return wrap [val] to optional type_ [to];
    } elif (([from.name] equals ["Optional"]) && ([to.name] equals ["Optional"])) {
        return cast optional [val] from [from] to [to];
    } elif ([to.name] equals ["Optional"]) {
        return cast [val] from [from] to optional [to];
    };

    abort ["There is no known way to cast values of type_ {} to {}" % [from].stringify % [to].stringify];
}

Val#do_Val_cmp[Val:a][Str:cmpop][Val:b] {
    assert type_ equality [a], [b];
    if ([a.type_].is_float) {
        return basic val [Bool_] [emit [compare float [[a].get] [["o"]+[cmpop]] [[b].get]]];
    } elif ([a.type_].is_poly) {
        return do_Val_cmp[[a].struct_][cmpop][[b].struct_];
    } else {
        if ([cmpop] equals ["eq"]) {
        } elif ([cmpop] equals ["ne"]) {
        } elif ([a.type_].is_unsigned_int) {
            cmpop = ["u"] + [cmpop];
        } else {
            cmpop = ["s"] + [cmpop];
        };
        return basic val [Bool_] [emit [compare int [[a].get] [cmpop] [[b].get]]];
    };
}

#require basic block termination {
    if (!current bb.is_terminated) {
        abort ["The current basic block must be terminated by this point"];
    };
}

Val#deep [Val:val] == null_ invert: [Bool:invert] {
    PHINode:result_phi = new PHI [Bool_];
    Label:end_label = new label;
    while ([val.type_.name] equals ["Optional"]) {
        Label:next_label = new label;
        [result_phi].add_incoming[c [!invert]];
        goto_ [[val] == null_] ? [end_label] : [next_label];
    [next_label]:;
        val = unwrap nullability [val];
    };
    [result_phi].add_incoming[c [invert]];
[end_label]:;
    return [result_phi].place_here;
}

Val#access field [W:nth] of [Val:val] as [Type_:type_] {
    return basic val [Pointer [type_]] [emit [
        GEP [[val.type_].pointee] [[val].get] [[IRValue] [
            make const [0] [64], make const [nth]
        ]]
    ]];
}

IRValue#extract field [W:nth] of [IRValue:agg] as [$IRType:type] {
    return emit [extract value [agg] [[W] [nth]] [type]];
}

Val#extract field [W:nth] of [Val:agg] as [Type_:type_] {
    return basic val [type_] [
        extract field [nth] of [[agg].get] as [[type_].ir]
    ];
}

IRValue#insert [IRValue:elem] into field [W:nth] of [IRValue:agg] {
    return emit [insert value [elem] [agg] [[W] [nth]]];
}

Val#insert [Val:elem] into field [W:nth] of [Val:agg] {
    return basic val [agg.type_] [
        insert [[elem].get] into field [nth] of [[agg].get]
    ];
}

$IRConstant#get_const_from_val[IRValue:val] {
    given (val.constant as $IRConstant:constant) {
        return constant;
    };
    abort ["Expected constant"];
}

$IRConstant#get_const_from_val[Val:val] {
    given (val.const as ConstVal:const_val) {
        return get_const_from_val[const_val.val];
    };
    abort ["Expected constant"];
}

IRValue#make_global type: [$IRType:ir_type] name: [Str?:provided_name] basic_init: [$IRConstant?:basic_init] str_init: [Str?:str_init] is_extern: [Bool:is_extern] is_const: [Bool:is_const] {
    Bool:insignificant_name = [provided_name].is_null;
    if (!provided_name) {
        Str:frmt_str = "global_{}_{}";
        if (is_const) {
            frmt_str = "const_{}_{}";
        };
        provided_name = frmt_str % get filename % [get state.globals].len;
    };
    Str:name = [provided_name].unwrap;
    for (IRGlobal:global_ in get state.globals) {
        if ([global_.register.name] equals [name]) {
            return make const [global_.register];
        };
    };
    IRRegister:register = IRRegister [true, name, IRPtrType []];
    Str?:linkage = null;
    if (is_extern) {
        if (basic_init || str_init) {
            abort ["Extern globals cannot have an initializer"];
        };
        linkage = "external";
    } elif (insignificant_name) {
        linkage = "private";
    };
    Bool:named_addr = !insignificant_name;
    [get state.globals].append[IRGlobal [
        register, linkage, named_addr, is_const, ir_type, basic_init, str_init
    ]];
    return make const [register];
}

L#index of field [Str:name] in [Struct_:struct_] {
    for (L:i enumerating struct_.fields) {
        if ([struct_.fields[i].name] equals [name]) {
            return i;
        };
    };
    abort ["Field {} not found in struct {}" % name % struct_.id];
}

Bool#[Val:val].is_global_ptr_const {
    if (![val.assignable].is_null) {
        return false;
    };
    given ([val].get.constant as IRGlobalPtrConstant:constant) {
        return true;
    };
    return false;
}

#[Val:val]._nonnull_check_ref[Val:rc] {
    assert [val] is nonnullable;
    assert [val] is ref;
    if ([val].is_global_ptr_const) {
        return;
    };
    if_ [[rc] eq [c L_ [0]]];
        Type_:type_ = val.type_;
        if ([type_.name] equals ["Array"]) {
            [val]._ref_free_array;
        } elif ([type_].is_poly) {
            [val]._ref_free_poly;
        } else {
            [val]._ref_free_struct[[[type_].get_struct]
                .expect["Don't know how to ref free val of type_ {}" % [type_].stringify]];
        };
    end block;
}

#[Val:outer_val]._ref_free_array {
    assert [outer_val] is array;

if (specialized fn [Null_] ["free"] [[Val] [outer_val]]) {
    Val:val = func arg [0];
    Type_:sub = val.type_.generics[0];

    Val:content = load [[val].content];
    if ([sub].is_ref) {
    Val:len = load [[val].length];
    Label:check = new label;
    Label:loop = new label;
    Label:final = new label;

    PHINode:i_phi = new PHI [L_];

    Val:initial_i = load [c L_ [0]];
    [i_phi].add_incoming[initial_i];

    goto_ [check];

[check]:;
    Val:i = [i_phi].place_here;
    goto_ [[i] lt [len]] ? [loop] : [final];

[loop]:;
    [load [[content].idx[i]]].decr_ref;
    Val:new_i = [i] + [c L_ [1]] bound: [true];
    [i_phi].add_incoming[new_i];
    goto_ [check];

[final]:;
    };

    free [content];
    free [val];

    ret void;
end fn;
};
}

#[Val:val]._ref_free_poly {
    assert [val] is poly;

    Val:free_fn = [[val].vtable].vtable_item[4];
    free_fn = bitcast [free_fn] to [FuncPtr [Null_] [[Type_] [val.type_]]];
    call [free_fn] [[Val] [val]];
}

#[Val:val]._ref_free_struct[Struct_:struct_] {
    if (!struct_.has_global_free_fn) {
        generate free fn for_ [struct_] private: [true];
    };
    Str:free_symbol = get free fn symbol for_ [struct_];
    extern fn [Null_] [free_symbol] [[Type_] [val.type_]] [[Val] [val]];
}

Str#get free fn symbol for_ [Struct_:struct_] {
    return "free_{}" % struct_.id_num;
}

Str#get vtable symbol for_ [Struct_:struct_] {
    return "vtable_{}" % struct_.id_num;
}
